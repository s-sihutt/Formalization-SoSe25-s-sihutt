import Mathlib.Tactic
import Mathlib.CategoryTheory.Limits.HasLimits

section review


/-
Last time we discussed three topics:

1. The value of `rintro`.

2. How Lean deals with `universes`.

3. How we can define structures in Lean.

-/

end review

section projects

/-
There are two options to proceed:
1. Regular option: Check the website.
2. Hard option: Talk to me.
-/

end projects

section mathlib
/-
Let's learn a little about `MathLib`
`MathLib` is the standard mathematical library for Lean4.
It contains most of the mathematics formalized in Lean4.
Part of setting up this project involved installing `MathLib`.

A single Lean file does not contain any particular definitions.
We hence load existing definitions from `MathLib` to use them in our file.
We include them in the beginning of the file using `import`.

For example, if you notice, every file until now has the first line:
`import Mathlib.Tactic`.

This loads `tactics` and various standard definitions and theorems from `MathLib`.

Let us see some examples.
-/

#check Group
#check Ring
#check Field
#check TopologicalSpace
#check MetricSpace
#check CategoryTheory.Category
#check ULift

-- If we go more specialized, we need more imports.
-- Here is an example from limits in category theory.
#check CategoryTheory.Limits.HasLimits

/-
Working on a project will mean familiarizing yourself with certain parts of `MathLib`,
relevant to your project.
-/
end mathlib

section groups_as_structures

-- `Groups` have already been defined in `MathLib`.
#check Group
-- We can unwind the definition.
#print Group
-- Let us try to reproduce such a definition by hand.

-- Here is a hands-on definition of a group:
structure Groupâ‚ (Î± : Type*) where
  mul : Î± â†’ Î± â†’ Î±
  one : Î±
  inv : Î± â†’ Î±
  mul_assoc : âˆ€ x y z : Î±, mul (mul x y) z = mul x (mul y z)
  mul_one : âˆ€ x : Î±, mul x one = x
  one_mul : âˆ€ x : Î±, mul one x = x
  inv_mul_cancel : âˆ€ x : Î±, mul (inv x) x = one
  mul_inv_cancel : âˆ€ x : Î±, mul x (inv x) = one

-- We can now as an example define the integers as a group:
def group_z : Groupâ‚ â„¤ where
  mul x y := x + y
  one := 0
  inv x := -x
  mul_assoc _ _ _ := add_assoc _ _ _
  one_mul := zero_add
  mul_one := add_zero
  inv_mul_cancel := fun _ => by ring
  mul_inv_cancel := fun _ => by ring

-- We can now use this group structure on `â„¤`:
#check group_z.mul
#check group_z.inv_mul_cancel
#check group_z.mul 2 (-3)

-- We can (for example) see that `â„¤` with the group structure `group_z` is commutative:
example (a b : â„¤) : group_z.mul a b = group_z.mul b a := by
  simp [group_z]
  ring

/-
We can use this approach to define any structure
(groups, rings, fields, topological spaces, etc.) in Lean.

However, this approach diverges from standard mathematical practice.

For example, we just write `â„` and the additional structure should be recognizable from the context.
-/



#check â„
-- In this line `â„` is a `group`:
#check (2 : â„) + (3 : â„)
#check Add.add (2 : â„) (3 : â„)
-- Here `â„` is a `field`:
#check (5 : â„) * (0.2 : â„)
-- Here `â„` is a `topological space` or maybe a `metric space`:
variable (f : â„ â†’ â„)
#check Continuous f

/-
If we followed our approach above, every one of these statements would require
explicitly stating the group structure, field structure, topology, etc.

Fortunately, there is a computer scientific solution in Lean.
Those are `classes` and `instances`.
-/
end groups_as_structures

section groups_as_classes
/-
We want to try to understand `structures`, `classes`, and `instances` in Lean.
In particular, the powerful tool of `type class inference`.

Let us restate a structure, we had seen before:
-/

def GroupR : Groupâ‚ â„ where
  mul x y := x + y
  one := 0
  inv x := -x
  mul_assoc _ _ _ := add_assoc _ _ _
  one_mul := zero_add
  mul_one := add_zero
  inv_mul_cancel := fun _ => by ring
  mul_inv_cancel := fun _ => by ring
/-
This definition is clearly the worst and we always have to
*remember* and *use* the name of the structure `GroupR`.
-/

#check Groupâ‚.mul GroupR (2:â„) (3: â„)

-- Lean knows what this structure is, because of `GroupR`.
example (x y : â„) : Groupâ‚.mul GroupR x y = x + y := by
  rfl

#eval Groupâ‚.mul GroupR (2:â„) (3:â„)

/-
We have seen before we can add `tags` to structures.
One such tag is `@[class]`.
-/

@[class] structure Groupâ‚‚ (Î± : Type*) where
  mul : Î± â†’ Î± â†’ Î±
  one : Î±
  inv : Î± â†’ Î±
  mul_assoc : âˆ€ x y z : Î±, mul (mul x y) z = mul x (mul y z)
  mul_one : âˆ€ x : Î±, mul x one = x
  one_mul : âˆ€ x : Î±, mul one x = x
  inv_mul_cancel : âˆ€ x : Î±, mul (inv x) x = one
  mul_inv_cancel : âˆ€ x : Î±, mul x (inv x) = one

/-
The `@[class]` tag tells Lean that this structure is a `class`.
This means that we can use `type class inference` to find the structure.
Concretely, we now structures as an `instance`.
We can then access the structure via `inferInstance`.
-/

instance : Groupâ‚‚ â„ where
  mul x y := x + y
  one := 0
  inv x := -x
  mul_assoc _ _ _ := add_assoc _ _ _
  one_mul := zero_add
  mul_one := add_zero
  inv_mul_cancel := fun _ => by ring
  mul_inv_cancel := fun _ => by ring

#check Groupâ‚‚.mul inferInstance (2:â„) (3:â„)

/-
We can add the option `trace.Meta.synthInstance true`
to see how Lean finds the instance.
-/

set_option trace.Meta.synthInstance true in
#check Groupâ‚‚.mul inferInstance (2:â„) (3:â„)

-- Even though we just wrote `inferInstance`, Lean knows what the operation is
example (x y : â„) : Groupâ‚‚.mul inferInstance x y = x + y := by
  rfl

#eval Groupâ‚‚.mul inferInstance (2:â„) (3:â„)

/-
That's a much easier than before, because we dont have to remember
and use the name of the structure `GroupR`.

However, this is still not what we usually do in mathematics.
We simply don't state any structure and just use the operations.

For that reason there is `class` which:
- a structure with the `@[class]` tag
- makes instances implicit.
-/

class Groupâ‚ƒ (Î± : Type*) where
  mul : Î± â†’ Î± â†’ Î±
  one : Î±
  inv : Î± â†’ Î±
  mul_assoc : âˆ€ x y z : Î±, mul (mul x y) z = mul x (mul y z)
  mul_one : âˆ€ x : Î±, mul x one = x
  one_mul : âˆ€ x : Î±, mul one x = x
  inv_mul_cancel : âˆ€ x : Î±, mul (inv x) x = one
  mul_inv_cancel : âˆ€ x : Î±, mul x (inv x) = one

instance : Groupâ‚ƒ â„ where
  mul x y := x + y
  one := 0
  inv x := -x
  mul_assoc _ _ _ := add_assoc _ _ _
  one_mul := zero_add
  mul_one := add_zero
  inv_mul_cancel := fun _ => by ring
  mul_inv_cancel := fun _ => by ring


#check Groupâ‚.mul GroupR (2:â„) (3:â„)
#check Groupâ‚‚.mul (inferInstance : Groupâ‚‚ â„) (2:â„) (3:â„)
#check Groupâ‚ƒ.mul (2:â„) (3:â„)

example (x y : â„) : Groupâ‚ƒ.mul x y = x + y := by
  rfl

#eval Groupâ‚ƒ.mul (2:â„) (3:â„)

-- We assume the existence of classes with `[` `]` brackets.
example (Î± : Type*) [Groupâ‚ƒ Î±] (x y : Î±) : Î± := Groupâ‚ƒ.mul x y
/-
Note here the Group structure is implicit, both
- In the assumption.
- In the parameter of the multiplication operation.
-/


/-
Here are some sample computations using:
- structures
- structures with `@[class]`
- classes
-/

example (Î± : Type*) (GrpA : Groupâ‚ Î±) (x y z w : Î±) : Groupâ‚.mul GrpA (Groupâ‚.mul GrpA x (Groupâ‚.mul GrpA y z)) w = Groupâ‚.mul GrpA (Groupâ‚.mul GrpA x y) (Groupâ‚.mul GrpA z w) := by
  simp [Groupâ‚.mul_assoc]

example (Î± : Type*) [GrpA : Groupâ‚‚ Î±] (x y z w : Î±) : Groupâ‚‚.mul GrpA (Groupâ‚‚.mul GrpA x (Groupâ‚‚.mul GrpA y z)) w = Groupâ‚‚.mul GrpA (Groupâ‚‚.mul GrpA x y) (Groupâ‚‚.mul GrpA z w) := by
  simp [Groupâ‚‚.mul_assoc]

example (Î± : Type*) [Groupâ‚ƒ Î±] (x y z w : Î±) : Groupâ‚ƒ.mul (Groupâ‚ƒ.mul x (Groupâ‚ƒ.mul y z)) w = Groupâ‚ƒ.mul (Groupâ‚ƒ.mul x y) (Groupâ‚ƒ.mul z w) := by
  simp [Groupâ‚ƒ.mul_assoc]

/-
This last one looks very similar to what we would usually hope for.
We can now even add custom notation and make it even more regular.

Here we use `infixl` to define the notation.
Here `infix` means that the notation is used between two elements.
Then ``infixl` means that the notation is left associative.
This means by definition `x Â·â‚ƒ y Â·â‚ƒ z` is interpreted as `x Â·â‚ƒ (y Â·â‚ƒ z)`.
The number `60` is the precedence of the operator.
-/

infix:60 " Â·â‚ƒ "   => Groupâ‚ƒ.mul

example (x y : â„) : x Â·â‚ƒ y = x + y := by
  rfl

example (Î± : Type*) [Groupâ‚ƒ Î±] (x y z w : Î±) : (x Â·â‚ƒ (y Â·â‚ƒ z)) Â·â‚ƒ w = (x Â·â‚ƒ y) Â·â‚ƒ (z Â·â‚ƒ w) := by
  simp [Groupâ‚ƒ.mul_assoc]

/-
Via instances and suitable notation we can write readable operations,
resembeling regular mathematical notation.

So `+` is just notation for `Add.add`, which is a class for `â„`.
-/

#check Add.add

/-
We can now see how all these desired structures are classes:

In this first example we use `AddGroup`, instead of `Groupâ‚`,
as we think of the operation in `Group` by default being multiplication.
-/
example : AddGroup â„ := inferInstance
example : Ring â„ := inferInstance
-- Here we use `noncomputable` to construct inverses.
noncomputable example : Field â„ := inferInstance
example : MetricSpace â„ := inferInstance
example : TopologicalSpace â„ := inferInstance

/-
We can now also witness our invented group
structures on `â„`:
-/

example : Groupâ‚ â„ := GroupR
example : Groupâ‚‚ â„ := inferInstance
example : Groupâ‚ƒ â„ := inferInstance

/-
Even beter, for a given instance, we can figure
out the name via the command `#synth`.
-/
#synth AddGroup â„
#synth Ring â„
#synth Field â„
#synth MetricSpace â„
#synth TopologicalSpace â„
#synth Groupâ‚‚ â„
#synth Groupâ‚ƒ â„

#check Real.instRing

/-
Here is a fancier example!
It defines a group structure on the set of self-equivalences.
We might explain this in detail later.
-/
instance {Î± : Type*} : Groupâ‚ƒ (Equiv.Perm Î±) where
  mul f g := Equiv.trans g f
  one := Equiv.refl Î±
  inv := Equiv.symm
  mul_assoc _ _ _ := (Equiv.trans_assoc _ _ _).symm
  one_mul := Equiv.trans_refl
  mul_one := Equiv.refl_trans
  inv_mul_cancel := Equiv.self_trans_symm
  mul_inv_cancel := Equiv.symm_trans_self

end groups_as_classes

section hierarchies
/-
We have now seen structures, classes, and instances.
Of course, there are many different structures.
Are they related and if yes, how?
The solution is to use `hierarchies`.

Let's try to understand this via an example.

Here is a simple class that assumes add a single term `one`.

Notice here the line `/-- The element one -/` is the doc-string.
That can be used to document the class for users.
-/
class Oneâ‚ (Î± : Type*) where
  /-- The element one -/
  one : Î±

/- Because of the class we have `[self : Oneâ‚ Î±]` -/
#check Oneâ‚.one -- Oneâ‚.one {Î± : Type} [self : Oneâ‚ Î±] : Î±

/-
Similar to operations, we can add notation for structures
Here, the tag `@[inherit_doc]`, means it has the same
doc-string as the original definition.
-/
@[inherit_doc]
notation "ğŸ™" => Oneâ‚.one

/- Remember for Lean this is just notation. -/
example {Î± : Type*} [Oneâ‚ Î±] : Î± := ğŸ™
example {Î± : Type*} [Oneâ‚ Î±] : (ğŸ™ : Î±) = ğŸ™ := rfl
example {Î± : Type*} [Oneâ‚ Î±] : Oneâ‚.one = (ğŸ™ : Î±) := rfl
/-
You can check here, that `(ğŸ™ : Î±)` is necessary
so Lean can infer the type.
-/

/-
We now separately proceed to define a class with a
binary operation, named `dia` for `diamond`.

Our eventual goal is that `ğŸ™` should be an identity element.
-/
class Diaâ‚ (Î± : Type*) where
  /-- Diamond operation -/
  dia : Î± â†’ Î± â†’ Î±

-- We saw before, we can introduce infix notation.
infixl:70 " â‹„ "   => Diaâ‚.dia

/-
We now have a binary operation, we want to make it associative.
Naively, we could just write out the operation and associativity.
-/
class Semigroupâ‚€ (Î± : Type*) where
  toDiaâ‚ : Diaâ‚ Î±
  /-- Diamond is associative -/
  dia_assoc : âˆ€ a b c : Î±, a â‹„ b â‹„ c = a â‹„ (b â‹„ c)

/-
Turns out this is a terrible idea!
The point of `class` is to synthesize instances.
But from this definition, Lean cannot recognize a semigroup.
-/

example {Î± : Type*} [Diaâ‚ Î±] (a b : Î±) : Î± := a â‹„ b
-- example {Î± : Type} [Semigroupâ‚€ Î±] (a b : Î±) : Î± := a â‹„ b

-- Beside `Semigroupâ‚€ Î±`, we need to add `Diaâ‚ Î±` by hand.
example {Î± : Type*} [Diaâ‚ Î±] [Semigroupâ‚€ Î±] (a b : Î±) : Î± := a â‹„ b
/-
This is of course not what we want.
Mathematicaly, we think of `Semigroupâ‚€` as a `Diaâ‚` with associativity,
meaning we are extending one structure via another.

One possible way to obtain this, is after the fact, add the
relevant connection to the `Diaâ‚` structure.
We achieve this via `attribute [instance]`.
This tells Lean that `Semigroupâ‚€` is an instance of `Diaâ‚`.
-/

attribute [instance] Semigroupâ‚€.toDiaâ‚

-- We now have a `Diaâ‚` instance.
example {Î± : Type*} [Semigroupâ‚€ Î±] : Diaâ‚ Î± := inferInstance

example {Î± : Type*} [Semigroupâ‚€ Î±] (a b : Î±) : Î± := a â‹„ b

/-
The better solution is to build in the connection from the start.
We can do this by using `extends` in the definition of `Semigroupâ‚`.
This means that by definition `Semigroupâ‚` is a `Diaâ‚` with associativity.
-/
class Semigroupâ‚‚ (Î± : Type*) extends toDiaâ‚ : Diaâ‚ Î± where
  /-- Diamond is associative -/
  dia_assoc : âˆ€ a b c : Î±, a â‹„ b â‹„ c = a â‹„ (b â‹„ c)

example {Î± : Type*} [Semigroupâ‚‚ Î±] (a b : Î±) : Î± := a â‹„ b

/-
Note the `toDiaâ‚` is redundant.
-/

class Semigroupâ‚ (Î± : Type*) extends Diaâ‚ Î± where
  /-- Diamond is associative -/
  dia_assoc : âˆ€ a b c : Î±, a â‹„ b â‹„ c = a â‹„ (b â‹„ c)

/-
Lean automatically uses `toDiaâ‚`.
Meaning it uses `to` with the previous class name.
-/
#check Semigroupâ‚.toDiaâ‚

example {Î± : Type*} [Semigroupâ‚ Î±] : Diaâ‚ Î± := inferInstance
example {Î± : Type*} [Semigroupâ‚ Î±] (a b : Î±) : Î± := a â‹„ b

/-
We now move one step further and add the identity element.
We do this by extending with `Semigroupâ‚` and `Oneâ‚` at the same time.
-/

class DiaOneClassâ‚ (Î± : Type*) extends Oneâ‚ Î±, Diaâ‚ Î± where
  /-- One is a left neutral element for diamond. -/
  one_dia : âˆ€ a : Î±, ğŸ™ â‹„ a = a
  /-- One is a right neutral element for diamond -/
  dia_one : âˆ€ a : Î±, a â‹„ ğŸ™ = a

-- In `DiaOneClassâ‚` we have instances of `Oneâ‚ Î±` and  `Diaâ‚ Î±`:
example {Î± : Type*} [DiaOneClassâ‚ Î±] : Diaâ‚ Î± := inferInstance
example {Î± : Type*} [DiaOneClassâ‚ Î±] : Oneâ‚ Î± := inferInstance
-- We hence use all the previous operations and definitions:
example {Î± : Type*} [DiaOneClassâ‚ Î±] : Î± := ğŸ™
example {Î± : Type*} [DiaOneClassâ‚ Î±] (a b : Î±) : Î± := a â‹„ b

/-
Let's use the `trace.Meta.synthInstance true` option
again to see what is going on.
-/
set_option trace.Meta.synthInstance true in
example {Î± : Type*} [DiaOneClassâ‚ Î±] (a b : Î±) : Prop := a â‹„ b = ğŸ™


/-
We have almost defined a monoid.
What is missing is combining associativity and the identity element.
We can do this by extending `Semigroupâ‚` and `DiaOneClassâ‚`.

The cool fact is that we do not need any additional information:
-/
class Monoidâ‚ (Î± : Type*) extends Semigroupâ‚ Î±, DiaOneClassâ‚ Î±

/-
Here is an interesting fact about this definition:
The `dia` operation is over-determined.
-/

example {Î± : Type*} [Semigroupâ‚ Î±] (a b : Î±) : Î± := a â‹„ b
example {Î± : Type*} [DiaOneClassâ‚ Î±] (a b : Î±) : Î± := a â‹„ b
example {Î± : Type*} [Monoidâ‚ Î±] (a b : Î±) : Î± := a â‹„ b

/-
Part of what `extends` does is to recognize common information
and make sure they coincide.
-/
example {Î± : Type*} [Monoidâ‚ Î±] :
  (Monoidâ‚.toSemigroupâ‚.toDiaâ‚.dia : Î± â†’ Î± â†’ Î±) = Monoidâ‚.toDiaOneClassâ‚.toDiaâ‚.dia := rfl

/-
This does not happen if we try to use naive definitions.
For example, here is a more direct definition of a monoid:
-/
class Monoidâ‚‚ (Î± : Type*) where
  toSemigroupâ‚ : Semigroupâ‚ Î±
  toDiaOneClassâ‚ : DiaOneClassâ‚ Î±

-- Now we, can check the following:
-- example {Î± : Type*} [Monoidâ‚‚ Î±] : (Monoidâ‚‚.toSemigroupâ‚.toDiaâ‚.dia : Î± â†’ Î± â†’ Î±) = Monoidâ‚‚.toDiaOneClassâ‚.toDiaâ‚.dia := rfl

/-
Why is everything okay in `Monoidâ‚` but not in `Monoidâ‚‚`?
The reason is that `extends` does not do what we tell it to do!
It has a mind on its own. When writing
`extends Semigroupâ‚ Î±, DiaOneClassâ‚ Î±`
Lean will check if the two structures have intersections and then remove them.

Let's see how this is done in practice:
-/
#check Monoidâ‚.mk
#check Monoidâ‚‚.mk

/-
Note, this does *not* mean there is no `Monoidâ‚.toDiaOneClassâ‚`
-/
#check Monoidâ‚.toSemigroupâ‚
#check Monoidâ‚.toDiaOneClassâ‚

set_option trace.Meta.synthInstance true in
example {Î± : Type*} [Monoidâ‚ Î±] (a b : Î±) : Î± := a â‹„ b

/-
We can now define the class `Invâ‚` for inverses.
Then extend `Monoidâ‚` with `Invâ‚` to define groups.
-/

class Invâ‚ (Î± : Type*) where
  /-- The inversion function -/
  inv : Î± â†’ Î±

@[inherit_doc]
postfix:max "â»Â¹" => Invâ‚.inv

class Groupâ‚„ (G : Type*) extends Monoidâ‚ G, Invâ‚ G where
  inv_dia : âˆ€ a : G, aâ»Â¹ â‹„ a = ğŸ™
  dia_inv : âˆ€ a : G, a â‹„ aâ»Â¹ = ğŸ™

/-
We can now start proving facts about groups.
Here is a simple example:
-/
example {M : Type*} [Groupâ‚„ M] {a : M} (u :  âˆ€ b : M, a â‹„ b = b) : a =  ğŸ™ := by
  calc
    a = a â‹„ ğŸ™:= by  rw [DiaOneClassâ‚.dia_one]
    _ = ğŸ™ := by rw [u ğŸ™]

/-
As we see, using the various properties of groups is very annoying.
We need to refer to the proper level of the structure.
For example, we write `DiaOneClassâ‚.dia_one` instead of just `dia_one`.

The solution is to use `export`.
-/

export DiaOneClassâ‚ (one_dia dia_one)
export Semigroupâ‚ (dia_assoc)
export Groupâ‚„ (inv_dia dia_inv)

example {M : Type*} [Groupâ‚„ M] {a : M} (u :  âˆ€ b : M, a â‹„ b = b) : a =  ğŸ™ := by
  calc
    a = a â‹„ ğŸ™:= by  rw [dia_one]
    _ = ğŸ™ := by rw [u ğŸ™]

end hierarchies

section making_implicit_explicit
/-
Let us end with a useful syntax.

When we define functions, then usually the input type and instances are implicit
-/
def gettingid {G: Type*} [Groupâ‚„ G] : G := ğŸ™

#check gettingid
-- This means we cannot apply anything to `gettingid`.
variable (G : Type*) (h : Groupâ‚„ G)
-- None of these options work:
-- #check gettingid G
-- #check gettingid h
-- #check gettingid G h

/-
Whenever we do need to do so,
the solution is to add a `@` in front of the function.
-/
#check @gettingid
#check @gettingid G h

end making_implicit_explicit

/-
We have seen how to define structures, classes, and instances.
We have seen how to use `extends` to build hierarchies.
Next time we discuss morphisms between structures.
-/
